plugins {
    id 'org.springframework.boot' version '2.2.7.RELEASE'
    id 'io.spring.dependency-management' version '1.0.9.RELEASE'
    id 'java'
    id 'pmd'
}

def defaultEncoding = 'UTF-8'
[compileJava, compileTestJava]*.options*.encoding = defaultEncoding

group = 'com.arianthox/Predictor/CoreEngine'
version = '0.0.1-SNAPSHOT'

repositories {
    mavenCentral()
    maven { url 'https://repo.spring.io/milestone' }
}

ext {
    set('springCloudVersion', "Hoxton.RC2")
    set('AkkaVersion', "2.6.6")
    set('AlpakkaKafkaVersion', "2.0.2")
    set('AlpakkaJSONVersion',"2.0.1")
    set('AlpakkaHTTPVersion',"10.2.0")
}

sourceCompatibility = JavaVersion.VERSION_11
targetCompatibility = JavaVersion.VERSION_11

configurations {
    developmentOnly
    runtimeClasspath {
        extendsFrom developmentOnly
    }
    compileOnly {
        extendsFrom annotationProcessor
    }
}

dependencyManagement {
    imports {
        mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
    }
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-security'
    implementation 'org.springframework.boot:spring-boot-starter-actuator'

    implementation 'org.springframework.cloud:spring-cloud-starter-netflix-eureka-client'
    implementation 'org.springframework.cloud:spring-cloud-starter-netflix-ribbon'
    implementation 'org.springframework.cloud:spring-cloud-starter-netflix-eureka-server'
    implementation 'org.springframework.cloud:spring-cloud-starter-openfeign'

    implementation 'io.springfox:springfox-swagger2:2.9.2'
    implementation 'io.springfox:springfox-swagger-ui:2.9.2'

    implementation "com.typesafe.akka:akka-stream-kafka_2.13:${AlpakkaKafkaVersion}"
    implementation "com.typesafe.akka:akka-stream_2.13:${AkkaVersion}"
    implementation "com.typesafe.akka:akka-actor-typed_2.13:${AkkaVersion}"
    implementation "com.typesafe.akka:akka-actor_2.13:${AkkaVersion}"

    implementation project(":commons")

    implementation("org.springframework.boot:spring-boot-starter-data-jpa"){
        exclude group: "org.apache.tomcat", module: "tomcat-jdbc"
    }
    runtimeOnly 'com.h2database:h2'

    implementation("com.lightbend.akka:akka-stream-alpakka-json-streaming_2.13:${AlpakkaJSONVersion}")
    implementation("com.typesafe.akka:akka-http_2.13:${AlpakkaHTTPVersion}")
    implementation("com.typesafe.akka:akka-http-spray-json_2.13:${AlpakkaHTTPVersion}")
    implementation("com.typesafe.akka:akka-http-jackson_2.13:${AlpakkaHTTPVersion}")

    implementation("com.fasterxml.jackson.datatype:jackson-datatype-jdk8:2.9.9")
    implementation("com.fasterxml.jackson.datatype:jackson-datatype-jsr310:2.9.9")

    compileOnly 'org.projectlombok:lombok'
    developmentOnly 'org.springframework.boot:spring-boot-devtools'
    annotationProcessor 'org.projectlombok:lombok'
    testImplementation('org.springframework.boot:spring-boot-starter-test') {
        exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
    }

}

sourceSets {
    main {
        java.srcDirs = ['src/main/java']
    }
}

tasks.withType(Pmd){
    reports{
        xml.enabled=true
        html.enabled=true
    }
}

pmd {
    ignoreFailures = true
    pmdTest.enabled=false
}

// make sure bootRun is executed when this task runs
task runStandalone(dependsOn:bootRun) {
    // TaskExecutionGraph is populated only after
    // all the projects in the build have been evaulated https://docs.gradle.org/current/javadoc/org/gradle/api/execution/TaskExecutionGraph.html#whenReady-groovy.lang.Closure-
    gradle.taskGraph.whenReady { graph ->
        logger.lifecycle('>>> Setting spring.profiles.active to Standalone')
        if (graph.hasTask(runStandalone)) {
            // configure task before it is executed
            bootRun {
                args = ["--spring.profiles.active=standalone"]
            }
        }
    }
}
